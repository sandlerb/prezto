#
# A monochrome theme that displays basic information.
#
# Authors:
#   Brian Tse <briankftse@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   Brett Sandler <sandler.brett@gmail.com>
#

function +vi-git-status() {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    hook_com[unstaged]='%F{red}●%f'
  fi
}

function prompt_brett_precmd {
  vcs_info
}

function prompt_brett_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_brett_precmd

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable git
  zstyle ':vcs_info:*' check-for-changes true
  zstyle ':vcs_info:*' stagedstr '%F{yellow}●%f'
  zstyle ':vcs_info:*' unstagedstr '%F{red}●%f'
  zstyle ':vcs_info:*' formats ' %F{white}[%F{yellow}%b%c%u%F{white}]%f'
  zstyle ':vcs_info:*' actionformats " - [%b%c%u|%F{cyan}%a%f]"
  zstyle ':vcs_info:*' branchformat '%b|%F{cyan}%r%f'
  zstyle ':vcs_info:git*+set-message:*' hooks git-status

  # Define prompts.
  PROMPT='%F{blue}%2~%f${vcs_info_msg_0_} » '
  RPROMPT=''
}

prompt_brett_setup "$@"

